plugins {
    id 'java-library'
    id "io.freefair.lombok" version "8.6"
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.3.1",
    allureVersion = "2.27.0"

dependencies {
    testImplementation(
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-selenide:2.21.0",
            "com.codeborne:selenide:6.19.1",
            "org.junit.jupiter:junit-jupiter:5.9.3",
            "org.slf4j:slf4j-simple:2.0.12",
            "org.assertj:assertj-core:3.26.0"
    )
    runtimeOnly "org.aspectj:aspectjweaver:1.9.22"
}

allure {
    report {
        version.set(allureVersion)
    }

    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
